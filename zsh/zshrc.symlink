# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# aliases for changing directories easily
alias d="dirs -v"
alias pd="pushd"
alias bd="popd > /dev/null; dirs -v"
alias r="pushd +1"
alias p="popd"

alias 1="pushd +1 > /dev/null; dirs -v"
alias 2="pushd +2 > /dev/null; dirs -v"
alias 3="pushd +3 > /dev/null; dirs -v"
alias 4="pushd +4 > /dev/null; dirs -v"
alias 5="pushd +5 > /dev/null; dirs -v"
alias 6="pushd +6 > /dev/null; dirs -v"
alias 7="pushd +7 > /dev/null; dirs -v"
alias 8="pushd +8 > /dev/null; dirs -v"
alias 9="pushd +9 > /dev/null; dirs -v"

alias d1="popd +1 > /dev/null; dirs -v"
alias d2="popd +2 > /dev/null; dirs -v"
alias d3="popd +3 > /dev/null; dirs -v"
alias d4="popd +4 > /dev/null; dirs -v"
alias d5="popd +5 > /dev/null; dirs -v"
alias d6="popd +6 > /dev/null; dirs -v"
alias d7="popd +7 > /dev/null; dirs -v"
alias d8="popd +8 > /dev/null; dirs -v"
alias d9="popd +9 > /dev/null; dirs -v"
